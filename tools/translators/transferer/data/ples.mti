
FP => AP
{
   if(Degree = superlative)
        Def := "el más";
   else if(Degree = comparative)
        Def := "más";        
}

P => A
{
   if (@.R = ż)
      Gender := fem;
   else
      Gender := masc;

   Num := @.L;
}

sub A::surface()
{
    if (Gender == fem)
       $gendercode := 4;
    else
       $gendercode := 1;

    if (Num == 2)
        $numcode := 2;
    else
        $numcode := 1;

    return pair(Equiv + "+adj", get_morpho_es(2, $gendercode, $numcode, 0, 0));
}

C => V
{
    Num := @.L;
    Tense := @.C;
    Person := @.O;
}

sub ::tense_to_code(t)
{
    if ($t = "teraźniejszy") {
        return 14;
    } else if ($t = "przyszły") {
        return 16;
    } else if ($t = "przeszły") {
        return 19;
    }
}

sub V::surface()
{
   return pair(Equiv + "+verb", get_morpho_es(tense_to_code(Tense), 0, Num, Person, 0));

}

sub GNP::default()
{
    %AdjectivePhrase [cat() = "AP"];
    %Noun [cat() = "N"];
    order %Noun < %AdjectivePhrase;
}

FR => GNP
{
    inherit("Interrogative",);
    inherit("Neg",);
    inherit("Gender",);
    inherit("Num",);

}

R => N
{
   # na razie rodzaj rzeczownika hiszpańskiego jest przenoszony z polskiego
   if (@.R = ż)
      Gender := fem;
   else
      Gender := masc;
   Num := @.L;
}

sub ::inflect_be_present(person, number, neg)
{
    $result := "";
    if($neg)
    {
        $result := "no ";
    }
    if($number = 1 and $person = 1)
        $result := $result + "soy";

    if($number = 1 and $person = 2)
        $result := $result + "eres";

    if($number = 1 and $person = 3)
        $result := $result + "es";

    if($number = 2 and $person = 1)
        $result := $result + "somos";

    if($number = 2 and $person = 2)
        $result := $result + "sois";

    if($number = 2 and $person = 3)
        $result := $result + "son";

    return $result;
}

sub N::surface()
{
    if (Gender == fem)
       $gendercode := 4;
    else
       $gendercode := 1;

    if (Num == 2)
        $numcode := 2;
    else
        $numcode := 1;

    return pair(Equiv + "+subst", get_morpho_es(0, $gendercode, $numcode, 0, 0));
}
